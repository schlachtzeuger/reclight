Example using an Arduino Due and switching the built-in LED
===========================================================

Required libraries
------------------
MIDIUSB (https://www.arduino.cc/en/Reference/MIDIUSB)
  - turns the Arduino into USB Slave mode and is then detected as a MIDI device
NeoPixel (https://learn.adafruit.com/adafruit-neopixel-uberguide/arduino-library-use)
  - connects external LEDs to the Arduino

Source
------
#include <Adafruit_NeoPixel.h>
#include "MIDIUSB.h"

#define PIN 6
#define STRIPSIZE 12

Adafruit_NeoPixel strip = Adafruit_NeoPixel(STRIPSIZE, PIN, NEO_GRB + NEO_KHZ800);

int led = 0;
bool idle = true;
unsigned long lastChange = millis();

void setup() {
  // setup internal led
  pinMode(LED_BUILTIN, OUTPUT);

  // setup strip with a fancy animation =)
  strip.begin();
  strip.setBrightness(25);
  for (int i = 0; i < STRIPSIZE; i++) {
    strip.setPixelColor(i, 100, 100, 100);
    delay(150);
    strip.show();
  }
  delay(500);
  all(strip.Color(0, 0, 0));
  strip.show();
}

unsigned long last = 0;

void loop() {
  moveLed(50);
  
  midiEventPacket_t rx;
  do {
    
    moveLed(50);
    
    rx = MidiUSB.read();
    if (rx.header != 0) {
      if (rx.byte1 == 0x90 && rx.byte2 == 0x5F) {
        idle = rx.byte3 != 0x7F;
      }
    }

    if (idle) {
      stepIdle();
    } else {
      stepRecording();
    }
  } while (rx.header != 0); 
  
}

void moveLed(unsigned long interval) {
  // we must not use delay() cause this may break the recording
  unsigned long now = millis();
  if (now - lastChange > interval) {
    lastChange = now;
    led = (led + 1) % STRIPSIZE;
  }
}

void stepIdle() {
  all(strip.Color(0, 255, 0));
  strip.show();
}

void stepRecording() {
 all(strip.Color(0, 0, 0));
 
 for (int i = 0; i < 4; i++) {
   int currentLed = (led + i) % STRIPSIZE;
   int color = (i+1)*50;
   strip.setPixelColor(currentLed, color, 0, 0);
 }

 strip.show(); 
}

void all(uint32_t color) {
  for (int i = 0; i < STRIPSIZE; i++) {
    strip.setPixelColor(i, color);
  }
}

What's missing
--------------
- =)
